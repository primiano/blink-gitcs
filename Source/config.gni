# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

if (is_android) {
  import("//build/config/android/config.gni")
} else {
  # TODO(brettw) remove this once && early-out is checked in.
  is_android_webview_build = false
}
if (cpu_arch == "arm") {
  import("//build/config/arm.gni")
} else {
  # TODO(brettw) remove this once && early-out is checked in.
  arm_version = 0
}

declare_args() {
  # Enables the Oilpan garbage-collection infrastructure.
  enable_oilpan = false

  # Set to true to enable the clang plugin that checks the usage of the Blink
  # garbage-collection infrastructure during compilation.
  blink_gc_plugin = false
  blink_gc_plugin_dump_graph = false
}

use_default_render_theme = use_aura

# Whether Android ARM or x86 build uses OpenMAX DL FFT. Currently only
# supported on Android ARMv7+, ia32 or x64 without webview. When enabled, this
# will also enable WebAudio support on Android ARM, ia32 and x64. Default is
# enabled. Whether WebAudio is actually available depends on runtime settings
# and flags.
use_openmax_dl_fft = is_android && !is_android_webview_build &&
  (cpu_arch == "x86" ||
   cpu_arch == "x64" ||
   (cpu_arch == "arm" && arm_version > 7))

# Set this to true to enable use of concatenated impulse responses for the HRTF
# panner in WebAudio.
# TODO(brettw) do we need this or can we assume its always on?
use_concatenated_impulse_responses = true
